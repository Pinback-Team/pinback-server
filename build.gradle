plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
}

allprojects {
    group = 'com.pinback'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.3"
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    // 공통 의존성
    dependencies {
        // Lombok
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'

        // Test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.withType(JavaCompile).configureEach {
        options.incremental = true
        options.compilerArgs += ['-Xlint:deprecation']
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        inputs.property("java.vendor", System.getProperty("java.vendor"))
        inputs.property("java.version", System.getProperty("java.version"))
    }
}

project(':api') {
    apply plugin: 'org.springframework.boot'

    bootJar {
        enabled = true
        archiveClassifier = ''
    }

    jar {
        enabled = false
    }
}

configure(subprojects.findAll { it.name != 'api' }) {
    bootJar {
        enabled = false
    }

    jar {
        enabled = true
        archiveClassifier = ''
    }
}
